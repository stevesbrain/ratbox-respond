if (word(2 $loadinfo()) != [pf]) {
	load -pf $word(1 $loadinfo());
	return;
};

#	Copyright (c) 2006 David B. Kratter (BlackJac@EFNet)

#	Version: 3.0.2006.01.29.1

#	Requires: EPIC4 or EPIC5

xdebug +new_math;

# Location and filename of the CHALLENGE respond binary
@ challenge_bin = [~/.irc/ratbox-respond];

# Location and filename of the CHALLENGE private key
@ challenge_key = [~/.irc/private.key];

# Require a CHALLENGE passphrase (0 = no | 1 = yes)
@ challenge_pass = 1;

alias challenge (nick default "$servernick()", void) {
	quote challenge $nick;
};

^on ^740 "*" {
	@ challenge.recv #= *1;
};

^on ^741 "*" {
	if (fexist($challenge_bin) == 1 && fexist($challenge_key) == 1) {
		^exec -name challenge $challenge_bin $challenge_key;
		if (challenge_pass) {
			input -noecho "Enter passphrase for CHALLENGE: " {
				^msg %challenge $*;
				^msg %challenge $challenge.recv;
			};
		} else {
			^msg %challenge \n;
			^msg %challenge $challenge.recv;
		};
	} else {
		xecho -b Unable to find "$after(-1 / $challenge_bin)" or "$after(-1 / $challenge_key)";
		@ challenge.recv = [];
	};
};

^on ^exec "challenge *" {
	if (*2) {
		xecho -b $1-;
	} else {
		quote challenge +$1;
	};
	@ challenge.recv = [];
};

^on ^exec_exit "challenge *" #;

# EOF
